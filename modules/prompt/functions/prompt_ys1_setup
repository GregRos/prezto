# ys1 - modified ys prompt
# Clean, simple, compatible and meaningful.
# Tested on Linux, Unix and Windows under ANSI colors.
# It is recommended to use with a dark background.
# Colors: black, red, green, yellow, *blue, magenta, cyan, and white.
#
# Mar 2013 Yad Smood (Author of the base theme for oh-my-zsh)
# Apr 2019 Siddharth Kannan
#
# Prompt format:
#
# # DIRECTORY k8s:context ns:namespace on git:BRANCH STATE C:LAST_EXIT_CODE
# $ COMMAND
#
# For example:
#
# # ~/work/notes k8s:mercari-dev ns:default on git:master x C:42
# $
#
# Related documents:
# http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html#Conditional-Substrings-in-Prompts
# http://aperiodic.net/phil/prompt/

function prompt_ys1_help {
  echo '
    ys1 is the prezto equivalent of the ys1 theme on oh-my-zsh

    Original theme:
    https://github.com/robbyrussell/oh-my-zsh/wiki/themes#ys1
  '
}

function prompt_ys1_precmd {
  if (( $+functions[git-info] )); then
    git-info
  fi

  if (( $+functions[kubernetes-info] )); then
    kubernetes-info
  fi
}

function prompt_ys1_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent sp subst)

  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  add-zsh-hook precmd prompt_ys1_precmd

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format '%b'
  zstyle ':prezto:module:git:info:clean' format ' %F{green}o%f'
  zstyle ':prezto:module:git:info:dirty' format ' %F{red}x%f'
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' ' %F{white}on git:%f%F{cyan}%b%f%D%C' \
    'rprompt' ''

  zstyle ':prezto:module:kubernetes:info:context' format ' k8s:%F{cyan}%C%f'
  zstyle ':prezto:module:kubernetes:info:namespace' format ' ns:%F{cyan}%N%f'

  PROMPT='
%{%F{blue}%}#%f \
%{%F{yellow}%}%~%f\
${kubernetes_info[context]}\
${kubernetes_info[namespace]}\
${git_info[prompt]}\
 \
%(?..C:%F{red}$?%f)
%{%F{red}%}$ %{$reset_color%}'

  RPROMPT=''
}

prompt_ys1_setup "$@"
